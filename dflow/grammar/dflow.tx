/*
 *            DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE
 *                    Version 2, December 2004
 *
 * Copyright (C) 2022 Panayiotou, Konstantinos <klpanagi@gmail.com>
 * Author: Panayiotou, Konstantinos <klpanagi@gmail.com>
 *
 * Everyone is permitted to copy and distribute verbatim or modified
 * copies of this license document, and changing it is allowed as long
 * as the name is changed.
 *
 *            DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE
 *   TERMS AND CONDITIONS FOR COPYING, DISTRIBUTION AND MODIFICATION
 *
 *  0. You just DO WHAT THE FUCK YOU WANT TO.
 */

/*
    dFlow Language
*/

dFlow:
    (
    ('entities'
        entities*=TrainableEntity
    'end')?

    ('synonyms'
        synonyms*=Synonym
    'end')?

    ('gslots'
        gslots*=GlobalSlot[',']
    'end')?

    ('triggers'
        triggers+=Trigger
    'end')?

    ('dialogues'
        dialogues+=Dialogue
    'end')?

    ('eservices'
        eservices*=EServiceDef
    'end')?

    ('access_controls'
        access_control=AccessControlDef
    'end')?
    )#
;

EServiceDef: EServiceDefHTTP;

EServiceDefHTTP:
    'EServiceHTTP' name=ID
        (  'verb:' verb=HTTPVerb
          'host:' host=STRING
          ('port:' port=INT)?
          ('path:' path=STRING)?
        )#
    'end'
;

// ENTITIY --------------------------------------------------------------
// ----------------------------------------------------------------------
Entity: TrainableEntity | PretrainedEntity;

TrainableEntity:
    'Entity' name=ID
        words+=Words[',']
    'end'
;

PretrainedEntity:
    'PERSON'        |
    'NORP'          |
    'FAC'           |
    'ORG'           |
    'GPE'           |
    'LOC'           |
    'PRODUCT'       |
    'EVENT'         |
    'WORK_OF_ART'   |
    'LAW'           |
    'LANGUAGE'      |
    'DATE'          |
    'TIME'          |
    'PERCENT'       |
    'MONEY'         |
    'QUANTITY'      |
    'ORDINAL'       |
    'CARDINAL'
;
// ----------------------------------------------------------------------
// ----------------------------------------------------------------------

Synonym:
    'Synonym' name=ID
        words+=Words[',']
    'end'
;

// TRIGGER --------------------------------------------------------------
// ----------------------------------------------------------------------
Trigger: Intent | Event;

Intent:
    'Intent' name=ID
        phrases+=IntentPhraseComplex[',']
    'end'
;

Event:
    'Event' name=ID
        uri=STRING
    'end'
;

IntentPhraseComplex: phrases+=IntentPhrase;

IntentPhrase:
    IntentPhraseStr |
    IntentPhraseSynonym |
    TrainableEntityRef |
    PretrainedEntityRef
;

IntentPhraseStr: STRING;

TrainableEntityRef: 'TE:' entity=[TrainableEntity|FQN|^entities*];

PretrainedEntityRef: 'PE:' entity=[PretrainedEntity|FQN|^entities*] ('[' refPreValues*=STRING[','] ']')?;

IntentPhraseSynonym: 'S:' synonym=[Synonym|FQN];
// ----------------------------------------------------------------------
// ----------------------------------------------------------------------

// DIALOGUE -------------------------------------------------------------
// ----------------------------------------------------------------------
Dialogue:
    'Dialogue' name=ID
        'on:' onTrigger+=[Trigger|FQN|^triggers][',']
        'responses:' responses+=Response[',']
    'end'
;

Response: ActionGroup | Form;

Form:
    'Form' name=ID
        params+=FormParam
    'end'
;

FormParam:
    name=ID ':' type=ParameterTypeDef '=' source=FormParamSource
;

FormParamRef: param=[FormParam|FQN|^dialogues*.responses.params];
FormParamIndex: FormParamRef('['ID('.'ID)*']')?;

FormParamSource: HRIParamSource | EServiceParamSource;

HRIParamSource:
    'HRI' '(' askSlot+=Text (',' '['extract+=ExtractionSource[','] ']')? ')'
;

ExtractionSource: ExtractFromEntity | ExtractFromIntent;
ExtractFromIntent: intent=[Trigger|FQN|^triggers*] ':' value=ParameterValue;
/* ExtractFromEntity: entity=[Entity|FQN|^entities*]; */
ExtractFromEntity: TrainableEntityRef | PretrainedEntityRef;


EServiceParamSource: EServiceCallHTTP;

EServiceCallHTTP:
    eserviceRef=[EServiceDef|FQN|eservices]'('
        (
        ('query=' '[' query_params*=EServiceParam[','] ']' ',')?
        ('header=' '[' header_params*=EServiceParam[','] ']' ',')?
        ('path=' '[' path_params*=EServiceParam[','] ']' ',')?
        ('body=' '[' body_params*=EServiceParam[','] ']' ',')?
        )#
    ')' ('[' response_filter=EServiceResponseFilter ']')?
;

EServiceParam: name=ID '=' value=ParameterValue;

ParameterValue:
    INT                 |
    FLOAT               |
    STRING              |
    BOOL                |
    List                |
    Dict                |
    EnvPropertyDef		|
    FormParamIndex      |
    GlobalSlotIndex     |
	Text
;

ParameterTypeDef:
    'int'   |
    'float' |
    'str'   |
    'bool'  |
    'list'  |
    'dict'
;

EServiceResponseFilter: ID('.'ID)*;
// ----------------------------------------------------------------------

// Global Slots ---------------------------------------------------------
// ----------------------------------------------------------------------
GlobalSlotValue: ParameterValue;
GlobalSlotType: ParameterTypeDef;
GlobalSlotRef: slot=[GlobalSlot|FQN|^gslots];
GlobalSlotIndex: FormParamRef('['ID('.'ID)*']')?;

GlobalSlot:
    name=ID ':' type=GlobalSlotType ('=' default=GlobalSlotValue)?
;
// ----------------------------------------------------------------------

// ACTIONS --------------------------------------------------------------
// ----------------------------------------------------------------------
ActionGroup:
    'ActionGroup' name=ID
        actions+=Action
    'end'
;

Action:
    SpeakAction     |
    FireEventAction |
    RESTCallAction  |
    SetFormSlot     |
    SetGlobalSlot
;

SpeakAction:
    'Speak' '(' text+=Text ')'
;

SetFormSlot:
    'SetFSlot' '(' slotRef=FormParamRef ',' value=ParameterValue ')'
;

SetGlobalSlot:
    'SetGSlot' '(' slotRef=GlobalSlotRef ',' value=ParameterValue ')'
;

FireEventAction:
    'FireEvent' '(' uri+=Text ',' msg=ParameterValue ')'
;

RESTCallAction: EServiceCallHTTP;
// ----------------------------------------------------------------------

// ACCESS_CONTROL -------------------------------------------------------
// ----------------------------------------------------------------------

AccessControlDef:
    (
    roles=Roles
    policies+=Policy
    path=Path
    (users=Users)?
    authentication=Authentication
    )#
;

Roles:
    'Roles'
        words+=Words[',']
        'default:'
            default=Words
    'end'
;

Policy:
    'Policy' name=ID
        'on:' actions+=Words[',']
        'roles:' roles+=Words[',']
    'end'
;

Path:
    'Path'
        path=STRING
    'end'
;

Users:
    'Users'
        roles+=UserRoles
    'end'
;

Authentication:
    'Authentication'
        'method:' method=AuthMethods
        (
        'slot_name:' slot_name=Words |
        (
        'token:' token=Words
        'channel:' channel=Words
        'signing_secret:' signing_secret = Words
        )
        )
    'end'
;

UserRoles: role=Words ':' users+=Words_ext[','];

AuthMethods: 'slot' | 'slack';

// ----------------------------------------------------------------------

// GENERAL --------------------------------------------------------------
// ----------------------------------------------------------------------
HTTPVerb:
    'GET'   |
    'POST'  |
    'PUT'
;

DictItem:
    name=ID ':' value=DictTypes
;

DictTypes:
    NUMBER | STRING | BOOL | Dict | List | FormParamIndex | GlobalSlotIndex
;

Dict:
    '{' items*=DictItem[','] '}'
;

List:
    '[' items*=ListElements[','] ']'
;

ListElements:
    NUMBER | STRING | BOOL | List | Dict | FormParamIndex | GlobalSlotIndex
;

Words:
    /[-\w ]*\b/
;

Words_ext:
    /[-\.\@\w ]*\b/
;

Text: TextStr | EnvPropertyDef | FormParamIndex | GlobalSlotIndex;

TextStr: STRING;

EnvPropertyDef: UserPropertyDef | SystemPropertyDef;
UserPropertyDef: 'USER:' property=[UserProperty|FQN];
SystemPropertyDef: 'SYSTEM:' property=[SystemProperty|FQN];

UserProperty:
    'NAME'      |
    'SURNAME'   |
    'AGE'       |
    'EMAIL'     |
    'PHONE'		|
    'CITY'		|
    'ADDRESS'
;

SystemProperty:
	'TIME'			|
	'LOCATION'		|
	'RANDOM_INT'	|
	'RANDOM_FLOAT'  |
    'HOSTNAME'      |
    'PUBLIC_IP'
;

// ----------------------------------------------------------------------
// ----------------------------------------------------------------------

// === Fully Qualified Names Rule ===
FQN: ID('.'ID)*;

// special rule for comments.
Comment:
    /\/\/.*$/
;
// ----------------------------------------------------------------------
